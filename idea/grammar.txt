program => statement* EOF


# statements
statement => exprStmt
           | varDecl
           | fnDecl
           | useStmt

varDecl => "var" IDENTIFIER ("=" expr )? ( "," IDENTIFIER ("=" expr )? )* ";"
fnDecl => "fn" IDENTIFIER function
useStmt => "use" IDENTIFIER ( "." ( ( "{" ( IDENTIFIER "," )* IDENTIFIER "}" ) | IDENTIFIER | "*" ) ) ";"

exprStmt => expr ";" | exprLike
exprLike => block
          | ifExpr
          | whileExpr
          | dowhileExpr
          | forExpr
          | breakExpr
          | continueExpr
          | returnExpr


ifExpr => "if" expr block ( "elif" expr block )* ( "else" block )?

whileExpr => "while" expr? block
dowhileExpr => "do" block "while" expr ";"
forExpr => "for" "(" IDENTIFIER "in" expr ")" block

breakExpr => "break" ";"        # todo: "break" expr ";"
continueExpr => "continue" ";"
returnExpr => "return" expr? ";"


# expressions
expr => range
range => ternary ( ( ".." | ".=" ) ternary )?

ternary => assignment ( "?" expr ":" ternary )?
assignment => ( IDENTIFIER ( "=" | "+=" | "-=" | "*=" | "/=" | "**=" | "%=" ) assignment ) | or

or => and ( "or" and )*
and => equality ( "and" equality )*

equality => comparison ( ( "==" | "!=" ) comparison )*
comparison => addition ( ( ">" | "<" | ">=" | "<=" ) addition )*

addition => times ( ( "-" | "+" ) times )*
times => unary ( ( "*" | "/" | "%" ) unary )*

unary => ( ( "!" | "-" ) unary ) | call
call => primary ( "(" args? ")" | "[" expr "]" | "." IDENTIFIER )*

primary => NUMBER
         | STRING
         | IDENTIFIER
         | "true"
         | "false"
         | "nil"
         | "(" expr ")"
         | array
         | maps
         | exprLike

block => "{" statement* "}"
array => "[" ( expr "," )* ( expr )? "]"
maps => "{{" ( expr ":" expr? "," )* ( expr ":" expr? )? "}}"

args => expr ( "," expr )* ( IDENTIFIER "=" expr ( "," IDENTIFIER "=" expr )* )?
params => IDENTIFIER ( "," IDENTIFIER )* ( IDENTIFIER "=" expr ( "," IDENTIFIER "=" expr )* )?

function => "(" params? ")" block


IDENTIFIER => [ _ a-z A-Z ]? [ _ a-z A-Z 0-9 ]*
NUMBER => [ 0-9 _ ]+ ( "." [ 0-9 _ ]+ )?
STRING => ( "\"" ( [ ^"\"" ]+ ) "\"" ) | ( "'" ( [ ^"'" ]+ ) "'" ) | ( "`"+ ( [ ^"`"+ ]+ ) "`"+ )
";" => SEMICOLON | RIGHT_BRACE | EOF